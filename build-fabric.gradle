plugins {
    id 'fabric-loom' version '1.6-SNAPSHOT'
}

version = project.modVersion
group = "yalter.mousetweaks"
archivesBaseName = "MouseTweaks-fabric-mc${project.fabricMcVersion}"

java.toolchain.languageVersion = JavaLanguageVersion.of(project.modJavaVersion)

repositories {
    maven {
        url "https://maven.terraformersmc.com/releases"
    }
}

sourceSets {
    main {
        java {
            exclude 'yalter/mousetweaks/forge'
            exclude 'yalter/mousetweaks/neoforge'
        }

        resources {
            exclude 'META-INF/mods.toml'
            exclude 'META-INF/neoforge.mods.toml'
        }
    }
}

loom {
    mixin.defaultRefmapName = 'mousetweaks.mixins.refmap.json'
}

dependencies {
    minecraft "com.mojang:minecraft:${project.fabricMcVersion}"
    mappings loom.officialMojangMappings()
    modImplementation "net.fabricmc:fabric-loader:${project.fabricLoaderVersion}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabricApiVersion}"

    modImplementation "com.terraformersmc:modmenu:${project.fabricModMenuVersion}"
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand([
                "version": project.modVersion,
                "modJavaVersion": project.modJavaVersion,
                "fabricMcVersion": project.fabricMcVersion,
                "fabricLoaderVersion": project.fabricLoaderVersion,
                "modCredits": project.modCredits,
                "modDescription": project.modDescription,
        ])
    }
}

java {
    withSourcesJar()
}
