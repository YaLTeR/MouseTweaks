plugins {
    id 'fabric-loom' version '1.6-SNAPSHOT'
}

def minecraftVersion = "1.20.6" // Used for output JAR filenames.

version = project.mod_version
group = "yalter.mousetweaks"
archivesBaseName = "MouseTweaks-fabric-mc${minecraftVersion}"

java.toolchain.languageVersion = JavaLanguageVersion.of(21)

repositories {
    maven {
        url "https://maven.terraformersmc.com/releases"
    }
}

sourceSets {
    main {
        java {
            exclude 'yalter/mousetweaks/forge'
            exclude 'yalter/mousetweaks/neoforge'
        }

        resources {
            exclude 'META-INF/mods.toml'
            exclude 'META-INF/neoforge.mods.toml'
        }
    }
}

loom {
    mixin.defaultRefmapName = 'mousetweaks.mixins.refmap.json'
}

dependencies {
    minecraft "com.mojang:minecraft:${minecraftVersion}"
    mappings loom.officialMojangMappings()
    modImplementation "net.fabricmc:fabric-loader:0.15.10"
    modImplementation "net.fabricmc.fabric-api:fabric-api:0.97.8+1.20.6"

    modImplementation 'com.terraformersmc:modmenu:10.0.0-beta.1'
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

java {
    withSourcesJar()
}
