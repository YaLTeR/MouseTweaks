buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net' }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
        classpath 'org.spongepowered:mixingradle:0.7.+'
    }
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.spongepowered.mixin'

def minecraftVersion = "1.18" // Used for output JAR filenames.

version = project.mod_version
group = "yalter.mousetweaks"
archivesBaseName = "MouseTweaks-forge-mc${minecraftVersion}"

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

sourceSets {
    main {
        java {
            exclude 'yalter/mousetweaks/fabric'
            exclude 'yalter/mousetweaks/ModMenuApiImpl.java'
        }

        resources {
            exclude 'fabric.mod.json'
        }
    }
}

mixin {
    add sourceSets.main, 'mousetweaks.mixins.refmap.json'
    config 'mousetweaks.mixins.json'
}

minecraft {
    mappings channel: 'official', version: '1.18'

    runs {
        client {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            mods {
                mousetweaks {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            mods {
                mousetweaks {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            // https://github.com/SpongePowered/Mixin/issues/462#issuecomment-791370319
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"

            args '--mod', 'mousetweaks', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')
            mods {
                mousetweaks {
                    source sourceSets.main
                }
            }
        }
    }
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.18-38.0.12'
    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'
}

jar {
    manifest.attributes([
            "Specification-Title"     : "Mouse Tweaks",
            "Specification-Vendor"    : "YaLTeR",
            "Specification-Version"   : "${project.version}",
            "Implementation-Title"    : project.name,
            "Implementation-Version"  : "${project.version}",
            "Implementation-Vendor"   : "YaLTeR",
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
    ])

    finalizedBy 'reobfJar'
}

task srcJar(type: Jar) {
    archiveClassifier.set("src")
    from sourceSets.main.allJava
}

task apiJar(type: Jar) {
    archiveClassifier.set("api")
    from(sourceSets.main.output) {
        include 'yalter/mousetweaks/api/**'
    }
}

artifacts {
    archives srcJar
    archives apiJar
}
