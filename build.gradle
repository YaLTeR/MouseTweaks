buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net' }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
    }
}
apply plugin: 'net.minecraftforge.gradle'

def minecraftVersion = "1.17.1" // Used for output JAR filenames.

version = project.mod_version
group = "yalter.mousetweaks"
archivesBaseName = "MouseTweaks-forge-mc${minecraftVersion}"

java.toolchain.languageVersion = JavaLanguageVersion.of(16)

sourceSets {
    main {
        java {
            exclude 'yalter/mousetweaks/forge/ConfigGui.java'
            exclude 'yalter/mousetweaks/forge/ConfigGuiFactory.java'
            exclude 'yalter/mousetweaks/fabric'
        }

        resources {
            exclude 'fabric.mod.json'
        }
    }
}

minecraft {
    mappings channel: 'official', version: '1.17.1'

    runs {
        client {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            mods {
                mousetweaks {
                    source sourceSets.main
                }
            }
        }
    }
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.17.1-37.0.2'
}

jar {
    manifest.attributes([
        "Specification-Title": "Mouse Tweaks",
        "Specification-Vendor": "YaLTeR",
        "Specification-Version": "${project.version}",
        "Implementation-Title": project.name,
        "Implementation-Version": "${project.version}",
        "Implementation-Vendor": "YaLTeR",
        "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
    ])

    finalizedBy 'reobfJar'
}

task srcJar(type: Jar) {
    archiveClassifier.set("src")
    from sourceSets.main.allJava
}

task apiJar(type: Jar) {
    archiveClassifier.set("api")
    from(sourceSets.main.output) {
        include 'yalter/mousetweaks/api/**'
    }
}

artifacts {
    archives srcJar
    archives apiJar
}
