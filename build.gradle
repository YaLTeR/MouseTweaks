buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
    }
}
apply plugin: 'net.minecraftforge.gradle'

version = "2.12"
group = "yalter.mousetweaks"
archivesBaseName = "MouseTweaks"

def minecraftVersion = "1.14.4" // Used for output JAR filenames.

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'

configurations {
    // Make Minecraft classes visible from the API classes.
    apiCompile.extendsFrom(compile)
}

sourceSets {
    api
    main {
        // Make API classes visible from the main classes.
        compileClasspath += sourceSets.api.output
        runtimeClasspath += sourceSets.api.output

        java {
            exclude 'yalter/mousetweaks/forge/ConfigGui.java'
            exclude 'yalter/mousetweaks/forge/ConfigGuiFactory.java'
        }
    }
}

minecraft {
    mappings channel: 'snapshot', version: '20190719-1.14.3'

    runs {
        client {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'

            mods {
                mousetweaks {
                    source sourceSets.main
                }
            }
        }
    }
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.14.4-28.1.39'
}

jar {
    classifier = "mc${minecraftVersion}"

    // Add API classes to the JAR.
    from sourceSets.api.output.classesDirs

    manifest {
        attributes([
            "Specification-Title": "Mouse Tweaks",
            "Specification-Vendor": "YaLTeR",
            "Specification-Version": "${version}",
            "Implementation-Title": project.name,
            "Implementation-Version": "${version}",
            "Implementation-Vendor": "YaLTeR",
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

task srcJar(type: Jar) {
    classifier = "mc${minecraftVersion}-src"
    from sourceSets.main.allJava
    from sourceSets.api.allJava
}

task apiJar(type: Jar) {
    classifier = "mc${minecraftVersion}-api"
    from sourceSets.api.output
}

artifacts {
    archives srcJar
    archives apiJar
}
