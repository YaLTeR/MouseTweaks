buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net' }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '[6.0,6.2)', changing: true
        classpath 'org.spongepowered:mixingradle:0.7.+'
    }
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.spongepowered.mixin'

def minecraftVersion = "1.20.2" // Used for output JAR filenames.

version = project.mod_version
group = "yalter.mousetweaks"
base.archivesName = "MouseTweaks-forge-mc${minecraftVersion}"

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

sourceSets {
    main {
        java {
            exclude 'yalter/mousetweaks/fabric'
            exclude 'yalter/mousetweaks/ModMenuApiImpl.java'
            exclude 'yalter/mousetweaks/neoforge'
        }

        resources {
            exclude 'fabric.mod.json'
            exclude 'META-INF/mods-neo.toml'
        }
    }
}

mixin {
    add sourceSets.main, 'mousetweaks.mixins.refmap.json'
    config 'mousetweaks.mixins.json'
}

minecraft {
    mappings channel: 'official', version: '1.20.2'

    runs {
        client {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            mods {
                mousetweaks {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            mods {
                mousetweaks {
                    source sourceSets.main
                }
            }
        }
    }
}

tasks.register("fixModsToml", Copy) {
    // Copy the mods-forge.toml file to mods.toml
    from "src/main/resources/META-INF"
    include "mods-forge.toml"
    rename "mods-forge.toml", "mods.toml"
    into "src/main/resources/META-INF"
}

// Make the fixModsToml task run before the processResources task
processResources.dependsOn(fixModsToml)

processResources {
    inputs.property "version", project.mod_version
    filesMatching("META-INF/mods.toml") {
        expand "version": project.mod_version
    }
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.20.2-48.0.13'
    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'
}

jar {
    manifest.attributes([
            "Specification-Title"     : "Mouse Tweaks",
            "Specification-Vendor"    : "YaLTeR",
            "Specification-Version"   : "${project.version}",
            "Implementation-Title"    : project.name,
            "Implementation-Version"  : "${project.version}",
            "Implementation-Vendor"   : "YaLTeR",
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
    ])

    finalizedBy 'reobfJar'
}

task srcJar(type: Jar) {
    archiveClassifier.set("src")
    from sourceSets.main.allJava
}

task apiJar(type: Jar) {
    archiveClassifier.set("api")
    from(sourceSets.main.output) {
        include 'yalter/mousetweaks/api/**'
    }
}

artifacts {
    archives srcJar
    archives apiJar
}
